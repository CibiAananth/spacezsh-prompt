#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

# Replaced by sz::section <color> <content> [prefix] [suffix]
# Backward compatible for old custom sections defined by users

# cat <<< "EOF"
#   API spaceship::section <color> [prefix] <content> [suffix] is deprecated.
#   Please use sz::section <color> <content> [prefix] [suffix]
# EOF

# Draw prompt section (bold is used as default)
# USAGE:
#   spaceship::section <color> [prefix] <content> [suffix]
function spaceship::section {
  local color content prefix suffix
  local result=""

  [[ -n $1 ]] && color="%F{$1}" || color="%f"
  prefix="$2"
  content="$3"
  suffix="$4"

  if [[ -z $3 && -z $4 ]]; then
    content="$2"; prefix=""
  fi

  if [[ ${_SS_DATA[prompt_opened]} == true ]] \
    && [[ $SPACESHIP_PROMPT_PREFIXES_SHOW == true ]] \
    && [[ -n "$prefix" ]]; then
    # set prefix bold
    result+="%{%B%}${prefix}%{%b%}"
  fi
  _SS_DATA[prompt_opened]="true"
  result+="%{%B${color}%}" # set color
  result+="$content"       # section content
  result+="%{%b%f%}"       # unset color

  if [[ $SPACESHIP_PROMPT_SUFFIXES_SHOW == true ]] \
    && [[ -n "$suffix" ]]; then
    # reset bold, if it was diabled before
    result+="%{%B%}${suffix}%{%b%}"
  fi

  # pass the result out with the global variable
  _SS_DATA[section_result]="$result"
}

spaceship::section "$@"