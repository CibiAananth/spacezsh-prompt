#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

#
# Execution time
#
# Execution time of the last command.

# ------------------------------------------------------------------------------
# Configuration
# ------------------------------------------------------------------------------

SPACESHIP_EXEC_TIME_SHOW="${SPACESHIP_EXEC_TIME_SHOW=true}"
SPACESHIP_EXEC_TIME_PREFIX="${SPACESHIP_EXEC_TIME_PREFIX="took "}"
SPACESHIP_EXEC_TIME_SUFFIX="${SPACESHIP_EXEC_TIME_SUFFIX="$SPACESHIP_PROMPT_DEFAULT_SUFFIX"}"
SPACESHIP_EXEC_TIME_COLOR="${SPACESHIP_EXEC_TIME_COLOR="yellow"}"
SPACESHIP_EXEC_TIME_ELAPSED="${SPACESHIP_EXEC_TIME_ELAPSED=2}"
SPACESHIP_EXEC_TIME_PRECISION="${SPACESHIP_EXEC_TIME_PRECISION=2}"

# ------------------------------------------------------------------------------
# Section
# ------------------------------------------------------------------------------

ss::exec_time() {
  [[ $SPACESHIP_EXEC_TIME_SHOW == false ]] && return

  if (( _SS_DATA[time_duration] >= SPACESHIP_EXEC_TIME_ELAPSED )); then
    # Print time in human readable format
    # For that use `strftime` and convert
    # the duration (float) to an seconds
    # (integer).
    # See http://unix.stackexchange.com/a/89748
    local humanReadableDuration
    if (( _SS_DATA[time_duration] > 3600 )); then
      humanReadableDuration=$(TZ=GMT; strftime '%H:%M:%S' $(( int(rint(_SS_DATA[time_duration])) )))
    elif (( _SS_DATA[time_duration] > 60 )); then
      humanReadableDuration=$(TZ=GMT; strftime '%M:%S' $(( int(rint(_SS_DATA[time_duration])) )))
    else
      # If the command executed in seconds, round to desired precision and append "s"
      humanReadableDuration=$(printf %.${SPACESHIP_EXEC_TIME_PRECISION}f%s ${_SS_DATA[time_duration]} s)
    fi

    ss::section \
      "$SPACESHIP_EXEC_TIME_COLOR" \
      "$humanReadableDuration" \
      "$SPACESHIP_EXEC_TIME_PREFIX" \
      "$SPACESHIP_EXEC_TIME_SUFFIX"
  fi
}

track-ss-autoload ss::exec_time_preexec ss::exec_time_precmd
typeset -ag ss_{chpwd,preexec,precmd}_functions
# Make sure hook exec_time 1st
ss_chpwd_functions+=( ss::exec_time_preexec )
ss_preexec_functions+=( ss::exec_time_preexec )
ss_precmd_functions+=( ss::exec_time_precmd )

ss::exec_time "$@"
