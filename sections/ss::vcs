#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

ss::set_default SPACESHIP_VCS_SHOW    "true"
# ss::set_default SPACESHIP_VCS_PREFIX "on "
ss::set_default SPACESHIP_VCS_PREFIX ""
ss::set_default SPACESHIP_VCS_SUFFIX "$SPACESHIP_PROMPT_DEFAULT_SUFFIX"
ss::set_default SPACESHIP_GIT_SYMBOL " "

ss::set_default SPACESHIP_VCS_BRANCH_SHOW   "true"
ss::set_default SPACESHIP_VCS_BRANCH_PREFIX "${SPACESHIP_GIT_SYMBOL}"
ss::set_default SPACESHIP_VCS_BRANCH_SUFFIX ""
ss::set_default SPACESHIP_VCS_BRANCH_COLOR  "magenta"

ss::set_default SPACESHIP_VCS_STATUS_SHOW   "true"
ss::set_default SPACESHIP_VCS_STATUS_PREFIX " ["
ss::set_default SPACESHIP_VCS_STATUS_SUFFIX "]"
ss::set_default SPACESHIP_VCS_STATUS_COLOR  "red"


function ss::vcs {
  # TODO: use following flags for color control
  _SS_DATA[vcs_workdir_half_dirty]=1
  _SS_DATA[vcs_workdir_dirty]=1

  local vcs_branch vcs_status
  local -a tmp

  vcs_info

  [[ -z $vcs_info_msg_0_ ]] && return

  tmp=("${(s. .)vcs_info_msg_0_}")

  _SS_DATA[section_result]=""
  ss::section \
    "$SPACESHIP_VCS_BRANCH_COLOR" \
    "$SPACESHIP_VCS_BRANCH_PREFIX${tmp[1]}$SPACESHIP_VCS_BRANCH_SUFFIX"
  vcs_branch="${_SS_DATA[section_result]}"

  if (( ${#tmp} > 1 )); then
    _SS_DATA[section_result]=""
    ss::section \
      "$SPACESHIP_VCS_STATUS_COLOR" \
      "$SPACESHIP_VCS_STATUS_PREFIX${(j..)tmp[2,-1]}$SPACESHIP_VCS_STATUS_SUFFIX"
    vcs_status="${_SS_DATA[section_result]}"
  fi

  ss::section \
    'white' \
    "${vcs_branch}${vcs_status}" \
    "$SPACESHIP_VCS_PREFIX" \
    "$SPACESHIP_VCS_SUFFIX"
}

# Init
track-ss-autoload vcs_info
zstyle ':vcs_info:*' enable git hg svn

# HACK/TODO ex. from my personal shell
zstyle ':vcs_info:svn*:*' actionformats '%c%u %F{red}| %a%f'
zstyle ':vcs_info:*' actionformats '%b %F{red}| %a%f'
zstyle ':vcs_info:hg*:*' branchformat '%b'
zstyle ':vcs_info:*' check-for-changes true
# zstyle ':vcs_info:svn*:*' formats %c%u # causing blank status
zstyle ':vcs_info:*' formats %b%c%u%m
zstyle ':vcs_info:hg*:*' get-bookmarks true
zstyle ':vcs_info:hg*:*' get-revision true
zstyle ':vcs_info:hg*+gen-hg-bookmark-string:*' hooks hg-bookmarks
zstyle ':vcs_info:*' stagedstr $' \u271a'   # ✚
zstyle ':vcs_info:*' unstagedstr $' \u25cf' # ●

# VCS_INFO HOOKS
zstyle ':vcs_info:git*+set-message:*' hooks \
  vcs-detect-changes git-untracked git-aheadbehind
zstyle ':vcs_info:hg*+set-message:*' hooks \
  vcs-detect-changes hg-branchhead
zstyle ':vcs_info:svn*+set-message:*' hooks \
  vcs-detect-changes svn-detect-changes

# Load hooks for vcs_info
track-ss-autoload "+vi-vcs-detect-changes" \
  "+vi-git-untracked" "+vi-git-aheadbehind" \
  "+vi-hg-branchhead" \
  "+vi-svn-detect-changes"

ss::vcs "$@"
