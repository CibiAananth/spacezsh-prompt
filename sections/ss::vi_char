#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

#
# Prompt character
#

# ------------------------------------------------------------------------------
# Configuration
# ------------------------------------------------------------------------------

SPACESHIP_CHAR_PREFIX="${SPACESHIP_CHAR_PREFIX=""}"
SPACESHIP_CHAR_SUFFIX="${SPACESHIP_CHAR_SUFFIX=""}"
SPACESHIP_CHAR_SYMBOL="${SPACESHIP_CHAR_SYMBOL="➜ "}"
SPACESHIP_CHAR_SYMBOL_ROOT="${SPACESHIP_CHAR_SYMBOL_ROOT="$SPACESHIP_CHAR_SYMBOL"}"
SPACESHIP_CHAR_SYMBOL_SECONDARY="${SPACESHIP_CHAR_SYMBOL_SECONDARY="..."}"
SPACESHIP_CHAR_COLOR_SUCCESS="${SPACESHIP_CHAR_COLOR_SUCCESS="green"}"
SPACESHIP_CHAR_COLOR_FAILURE="${SPACESHIP_CHAR_COLOR_FAILURE="red"}"
SPACESHIP_CHAR_COLOR_SECONDARY="${SPACESHIP_CHAR_COLOR_SECONDARY="yellow"}"

SPACESHIP_VI_MODE_SHOW="${SPACESHIP_VI_MODE_SHOW=true}"
SPACESHIP_VI_MODE_INSERT="${SPACESHIP_VI_MODE_INSERT="❯ "}"
SPACESHIP_VI_MODE_NORMAL="${SPACESHIP_VI_MODE_NORMAL="❮ "}"
SPACESHIP_VI_MODE_CURSOR_CHANGE="${SPACESHIP_VI_MODE_CURSOR_CHANGE="false"}"

# ------------------------------------------------------------------------------
# Section
# ------------------------------------------------------------------------------

# Paint $PROMPT_SYMBOL in red if previous command was fail and
# paint in green if everything was OK.
ss::vi_char() {
  local color char

  if [[ $RETVAL -eq 0 ]]; then
    color="$SPACESHIP_CHAR_COLOR_SUCCESS"
  else
    color="$SPACESHIP_CHAR_COLOR_FAILURE"
  fi

  # http://zsh.sourceforge.net/Doc/Release/Zsh-Line-Editor.html#User_002dDefined-Widgets
  if [[ $SPACESHIP_VI_MODE_SHOW == true ]] && [[ -n $KEYMAP ]]; then
    char="${SPACESHIP_VI_MODE_INSERT}"

    case "${KEYMAP}" in
      main|viins)
      char="${SPACESHIP_VI_MODE_INSERT}"
      ;;
      vicmd)
      char="${SPACESHIP_VI_MODE_NORMAL}"
      ;;
    esac

  else
    if [[ $UID -eq 0 ]]; then
      char="$SPACESHIP_CHAR_SYMBOL_ROOT"
    else
      char="$SPACESHIP_CHAR_SYMBOL"
    fi
  fi

  ss::section \
    "$color" \
    "$char" \
    "$SPACESHIP_CHAR_PREFIX" \
    "$SPACESHIP_CHAR_SUFFIX"
}

# ------------------------------------------------------------------------------
# Helpers
# ------------------------------------------------------------------------------

# Temporarily switch to vi-mode
ss::vi_mode_enable() {
  local item

  # http://zsh.sourceforge.net/Doc/Release/User-Contributions.html#Manipulating-Hook-Functions
  # http://zsh.sourceforge.net/Doc/Release/Zsh-Line-Editor.html#Special-Widgets
  track-ss-autoload add-zle-hook-widget
  track-ss-autoload ss::vi_mode_cursor_switch

  for item in keymap-select line-init line-finish; do
    track-ss-autoload ss::vi-mode-${item}
    add-zle-hook-widget -Uz $item ss::vi-mode-${item}
  done

  bindkey -v
}

# Temporarily switch to emacs-mode
ss::vi_mode_disable() {
  local item

  for item in keymap-select line-init line-finish; do
    add-zle-hook-widget -D ss::vi-mode-${item}
  done
  add-zle-hook-widget -D ss::vi_mode_cursor_switch

  bindkey -e
}

if [[ $SPACESHIP_VI_MODE_SHOW == true ]]; then
  ss::vi_mode_enable
  # post setup: cleanup conflicting ZLE widgets
  track-ss-autoload ss::vi_mode_precmd
  add-zsh-hook precmd ss::vi_mode_precmd
fi

ss::vi_char "$@"
