#!/usr/bin/env zsh
# vim: ft=zsh fdm=marker foldlevel=0 sw=2 ts=2 sts=2 et

#
# Google Cloud Platform (gcloud)
#
# gcloud is a tool that provides the primary command-line interface to Google Cloud Platform.
# Link: https://cloud.google.com/sdk/gcloud/

# ------------------------------------------------------------------------------
# Configuration
# ------------------------------------------------------------------------------

SPACESHIP_GCLOUD_SHOW="${SPACESHIP_GCLOUD_SHOW=true}"
SPACESHIP_GCLOUD_PREFIX="${SPACESHIP_GCLOUD_PREFIX="using "}"
SPACESHIP_GCLOUD_SUFFIX="${SPACESHIP_GCLOUD_SUFFIX="$SPACESHIP_PROMPT_DEFAULT_SUFFIX"}"
SPACESHIP_GCLOUD_SYMBOL="${SPACESHIP_GCLOUD_SYMBOL="☁️  "}"
SPACESHIP_GCLOUD_COLOR="${SPACESHIP_GCLOUD_COLOR="26"}"

# ------------------------------------------------------------------------------
# Section
# ------------------------------------------------------------------------------

# Shows active gcloud configuration.
sz::gcloud() {
  [[ $SPACESHIP_GCLOUD_SHOW == false ]] && return

  # Check if the glcoud-cli is installed
  (( $+commands[gcloud] )) || return

  local config_dir active_config active_config_file project gcloud_text
  # Set the gcloud config base dir
  config_dir="${CLOUDSDK_CONFIG:-$HOME/.config/gcloud}"

  # Check if there is an active config
  [[ -f "${config_dir}/active_config" ]] || return

  # Reads the current config from the file
  active_config="$(head -n1 ${config_dir}/active_config)"

  # Check the active config file exists
  active_config_file=${config_dir}/configurations/config_${active_config}
  [[ -f ${active_config_file} ]] || return

  # Reads the current project from the active config file
  project=$(command sed -n 's/project = \(.*\)/\1/p' ${active_config_file})

  # Sets the prompt text to `active-config/active-project`
  gcloud_text="${active_config}/${project}"

  # Show prompt section
  sz::section \
    "$SPACESHIP_GCLOUD_COLOR" \
    "${SPACESHIP_GCLOUD_SYMBOL}${gcloud_text}" \
    "$SPACESHIP_GCLOUD_PREFIX" \
    "$SPACESHIP_GCLOUD_SUFFIX"
}

sz::gcloud "$@"
